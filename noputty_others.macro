icon saucegeyser
scrollwhendone
#pass certain monsters to the consult script
if monstername gremlin
	goto gremlinfight
endif
if monstername guy made of bees
	#kill if possible
	if hascombatitem jam band flyers
		if !hascombatitem antique hand mirror
			use jam band flyers
		endif
		if hascombatitem antique hand mirror
			use jam band flyers, antique hand mirror
		endif
	endif
	if hascombatitem rock band flyers
		if !hascombatitem antique hand mirror
			use rock band flyers
		endif
		if hascombatitem antique hand mirror
			use rock band flyers, antique hand mirror
		endif
	endif
endif

abort hppercentbelow 20

if gotjump
	if moxieclass && !hppercentbelow 90
		pickpocket
		if !hppercentbelow 90
			pickpocket
		endif
	endif
endif

#spell + item blockers
if monstername *Naughty Sorceress* || monstername *bonerdagon
	goto kill_the_mob
endif

#unstunnable
if (monstername oil baron)
	#suck out oil
	if monstername oil * && hascombatitem duskwalker syringe
		use duskwalker syringe
	endif
	goto kill_the_mob
endif

if hascombatitem rock band flyers || hascombatitem jam band flyers
	if hascombatitem rock band flyers
		use rock band flyers
	endif
	if hascombatitem jam band flyers
		use jam band flyers
	endif
endif

if monstername * pirate
	if hascombatitem The Big Book of Pirate Insults || hascombatitem Massive Manual of Marauder Mockery
		if hascombatitem The Big Book of Pirate Insults
			use The Big Book of Pirate Insults
		endif
		if hascombatitem Massive Manual of Marauder Mockery
			use Massive Manual of Marauder Mockery
		endif
	endif
	#banish clingy pirate
	if monstername clingy pirate && hascombatitem cocktail napkin
		use cocktail napkin
	endif
	goto generic_spells2
endif

mark generic_spells2

#banish others
if (monstername  A.M.C. gremlin || monstername  Sabre-toothed goat || monstername  crusty pirate || monstername  tomb asp || monstername  bullet bill || monstername  senile lihc || monstername  procrastination giant || monstername pygmy) && (hasskill banishing shout || hasskill howl of the alpha || hasskill creepy grin || hasskill give your opponent the stinkeye || hascombatitem divine champagne popper || hascombatitem crystal skull || hascombatitem harold's bell)
	if hascombatitem crystal skull
		use crystal skull
	endif
	if hascombatitem harold's bell
		use harold's bell
	endif
	if hasskill Batter Up
		skill Batter Up
	endif
	if hascombatitem short writ of habeas corpus && (monstername pygmy shaman || monstername pygmy witch lawyer || monstername pygmy orderlies || monstername pygmy witch nurse || monstername pygmy witch surgeon || monstername pygmy headhunter || monstername drunk pygmy)
		use short writ of habeas corpus
	endif
endif

mark kill_the_mob	
if hasskill steal accordion
	skill steal accordion
endif

	#moxie kill
	if moxieclass
		#build up momentum
		if discobandit
			if hasskill stealth mistletoe
				skill stealth mistletoe
			endif
			if hasskill Disco Dance of Doom
				skill Disco Dance of Doom
			endif
			if hasskill Disco Dance II: Electric Boogaloo
				skill Disco Dance II: Electric Boogaloo
			endif
			if hasskill Disco Dance 3: Back in the Habit
				skill Disco Dance 3: Back in the Habit
			endif
		endif
		if accordionthief && hasskill Accordion Bash
			skill Accordion Bash
		endif
		if hasskill throw shield
			skill throw shield
		endif
		while !missed 2
			attack
		endwhile
		abort
	endif
	#muscle kill
	if muscleclass
		if sealclubber &&  hasskill club foot
			#should really be only with fury
			skill club foot
		endif
		if turtletamer && hasskill shell up
			skill shell up
		endif
		if hasskill throw shield
			skill throw shield
			attack
		endif

		while hasskill cleave
			skill cleave
		endwhile
		while hasskill ravenous pounce && !monstername *naughty sorceress
			skill rave
		endwhile
		while hasskill shieldbutt && !monstername *naughty sorceress
			skill shieldbutt
		endwhile

		while !missed 2
			attack
		endwhile
		abort
	endif
	#myst kill
	if mysticalityclass
		#saucespam
		if sauceror
			if hasskill Curse of Weaksauce
				skill Curse of Weaksauce
			endif
			if hasskill throw shield
				skill throw shield
			endif
			
			while !missed 2
				skill Saucegeyser
			endwhile 
		endif
		#pastamancer
		if hasskill Curse of the Thousand Islands
			skill Curse of the Thousand Islands
		endif
		if hasskill entangling noodles
			skill entangling noodles
		endif
		if hasskill weapon of the pastalord
			if hasskill throw shield
				skill throw shield
			endif
			skill weapon of the pastalord
			
			while !missed 2
				skill weapon of the pastalord
			endwhile 
		endif
	endif
abort



mark gremlinfight
	#banish
	if monstername A.M.C. gremlin
		if hasskill banishing shout
			skill banishing shout
		endif
		if hasskill howl of the alpha
			skill howl of the alpha
		endif
		if hasskill creepy grin
			skill creepy grin 
		endif
		if hasskill give your opponent the stinkeye
			skill give your opponent the stinkeye
		endif
		if hascombatitem divine champagne popper
			use divine champagne popper
		endif
		if hascombatitem crystal skull
			use crystal skull
		endif
		if hascombatitem harold's bell
			use harold's bell
		endif
		runaway
	endif
	if match "whips out"
		use molybdenum magnet
	endif
	if match "a bombing run over your head" || match "eyeball-peeler" || match "bites you in the fibula" || match "picks a beet off of itself" || match "picks a radish off of itself" || pastround 23
		if hascombatitem rain-doh
			use rain-doh indigo cup
			use rain-doh blue balls
		endif
		goto kill_the_mob
	endif
	if hppercentbelow 50
		if hasskill devour minions
			skill devour minions
		endif
	endif
	use spectre scepter
goto gremlinfight

