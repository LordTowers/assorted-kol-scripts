icon saucegeyser
scrollwhendone
#pass certain monsters to the consult script
if monstername gremlin
	goto gremlinfight
endif
if monstername guy made of bees
	#kill if possible
	if hascombatitem jam band flyers
		if !hascombatitem antique hand mirror
			use jam band flyers
		endif
		if hascombatitem antique hand mirror
			use jam band flyers, antique hand mirror
		endif
	endif
	if hascombatitem rock band flyers
		if !hascombatitem antique hand mirror
			use rock band flyers
		endif
		if hascombatitem antique hand mirror
			use rock band flyers, antique hand mirror
		endif
	endif
endif

abort hppercentbelow 20

if monstername C. H. U. M. Chieftain || monstername Knob Goblin Elite Guard Captain || monstername Knob Goblin Embezzler || monstername Hustled Spectre || monstername Baa'baa'bu'ran || monstername Bad ASCII Art || monstername 7-Foot Dwarf Foreman || monstername C.A.R.N.I.V.O.R.E. Operative || monstername Glass of Orange Juice || monstername Neil || monstername Slow Talkin' Elliot || monstername Zim Merman || monstername War Frat Streaker || monstername Brutus, the Toga-Clad Lout || monstername Danglin' Chad || monstername Monty Basingstoke-Pratt, IV || monstername Next-Generation Frat Boy
	abort Special mob, putty it
endif

if gotjump
	if moxieclass && !hppercentbelow 90
		pickpocket
		if !hppercentbelow 90
			pickpocket
		endif
	endif
	if mysticalityclass && !hppercentbelow 90
		summonspirit
	endif
endif
	
#meat shield bosses
while (monstername The Big Wisniewski || monstername The Man || monstername *Naughty Sorceress* || monstername lord spookyraven || monstername protector spectre) && hasskill meat shield && !haseffect meat shielded
	skill meat shield
endwhile

if !monstername *Naughty Sorceress* && !monstername *bonerdagon
	if !(monstername black ops bugbear || monstername ancient unspeakable bugbear || monstername battlesuit bugbear type || monstername trendy bugbear chef || monstername bugaboo || monstername bugbear scientist)

		if hascombatitem rain-doh blue balls 
			use rain-doh blue balls
		endif
		
		if hasskill intimidating bellow
			skill intimidating bellow
		endif
		
		if hascombatitem rock band flyers
			use rock band flyers
		endif
		if hascombatitem jam band flyers
			use jam band flyers
		endif

		if monstername * pirate
			if hascombatitem The Big Book of Pirate Insults
				use The Big Book of Pirate Insults
			endif
			if hascombatitem Massive Manual of Marauder Mockery
				use Massive Manual of Marauder Mockery
			endif
		endif

		if hasskill transcendent olfaction && !haseffect on the trail          &&    (monstername dairy goat || monstername tomb rat || monstername blooper || monstername Hellion || monstername Bob Racecar || monstername Quiet Healer || monstername Goth Giant || monstername Blur || monstername Green Ops Soldier || monstername Zombie waltzers || monstername Shaky Clown || monstername Knott Yeti || monstername Hellion || monstername Violent Fungus || monstername Endless Conference Call || monstername * survivor)
			skill transcendent olfaction
		endif

		if hasskill Fire a badly romantic arrow&&  !monstername Battlie Knight Ghost && !monstername ghost miner && !monstername Claybender Sorcerer Ghost && !monstername Dusken Raider Ghost && !monstername Space Tourist Explorer Ghost && !monstername Whatsian Commando Ghost && (monstername lobsterfrogman || monstername ghost || monstername bricko bat || monstername angry bassist || monstername blue-haired girl || monstername evil ex-girlfriend || monstername peeved roommate || monstername random scenester || monstername black crayon * || monstername smut orc pervert)
			skill Fire a badly romantic arrow
		endif
		
		if sealclubber
			#should really be only with fury
			if hasskill club foot
				skill club foot
			endif
		endif
		if hasskill entangling noodles
			skill entangling noodles
		endif
	endif #!monstername black ops bugbear && monstername !ancient unspeakable bugbear && !monstername battlesuit bugbear type && !monstername trendy bugbear chef  && !monstername bugaboo  && !monstername bugbear scientist
	
	if hascombatitem quantum nanopolymer spider web && monstername bugbear scientist
		use quantum nanopolymer spider web
	endif
	
	if hascombatitem Drone self-destruct chip && monstername liquid metal bugbear
		use Drone self-destruct chip
	endif
	
endif

if monstername * Angry cavebugbear
	#if too many very's pacify it
	if monstername very very very very very angry cavebugbear
		use pacification grenade
	endif
	if hasskill transcendent olfaction && !haseffect on the trail
		skill transcendent olfaction
	endif
endif
	
if  !monstername *Naughty Sorceress* && !monstername *bonerdagon 
	if hasskill feed
		skill feed
	endif
	
	if hasskill spring raindrop attack
		skill spring raindrop attack
		if hasskill summer siesta
			skill summer siesta
		endif
	endif


	#smash and graaagh some monsters
	if hasskill infectious bite
		if hasskill smash & graaagh && (monstername filthworm || monstername BL imp || monstername CH imp || monstername PR imp || monstername inkubus || monstername suckubus || monstername serialbus || monstername Gnollish Gearhead || monstername irate mariachi || monstername handsome mariachi || monstername briefcase bat || monstername raver giant || monstername furry giant || monstername goth giant || monstername sassy pirate || monstername swarthy pirate || monstername dairy goat || monstername spiny skelelton || monstername toothy sklelton || monstername blur || monstername bob racecar || monstername racecar bob || monstername grungy pirate || monstername war pledge || monstername frat warrior drill sergeant || monstername baseball bat || monstername doughbat || monstername perpendicular bat || monstername skullbat || monstername vampire bat || monstername batrat || monstername ratbat || monstername beanbat) && !monstername the queen filthworm
			skill smash & graaagh
		endif
		
		#do infectious bite
		if hasskill infectious bite && !beenhit 1 && !hppercentbelow 80 && !hasskill wrath of the volcano god
			skill infectious bite
		endif
	endif

	if hascombatitem rain-doh indigo cup && !monstername *Naughty Sorceress* && !monstername Bonerdagon
		use rain-doh indigo cup
	endif
	
	#banish clingy pirate
	if monstername clingy pirate && hascombatitem cocktail napkin
		use cocktail napkin
	endif
	
	#banish others
	if (monstername  A.M.C. gremlin || monstername  Sabre-toothed goat || monstername  crusty pirate || monstername  tomb asp || monstername  bullet bill || monstername  senile lihc || monstername  procrastination giant) && (hasskill banishing shout || hasskill howl of the alpha || hasskill creepy grin || hasskill give your opponent the stinkeye || hascombatitem divine champagne popper || hascombatitem crystal skull || hascombatitem harold's bell)
		if hasskill banishing shout
			skill banishing shout
		endif
		if hasskill howl of the alpha
			skill howl of the alpha
		endif
		if hasskill creepy grin
			skill creepy grin 
		endif
		if hasskill give your opponent the stinkeye
			skill give your opponent the stinkeye
		endif
		if hascombatitem divine champagne popper
			use divine champagne popper
		endif
		if hascombatitem crystal skull
			use crystal skull
		endif
		if hascombatitem harold's bell
			use harold's bell
		endif
	endif

	#if we have a multi banishing skill
	if monstername chatty pirate || monstername tomb servant || monstername slick lihc || monstername possibility giant && (hasskill banishing shout || hasskill howl of the alpha)
		if hasskill banishing shout
			skill banishing shout
		endif
		if hasskill howl of the alpha
			skill howl of the alpha
		endif
	endif
	
	#suck out oil
	if monstername oil * && hascombatitem duskwalker syringe
		use duskwalker syringe
	endif
	
	#correct juju mask buff
	if moxieclass 
		if hasskill wrath of the Volcano god
			skill wrath of the Volcano god
			if hasskill sing
				skill sing
			endif
			if !hasskill sing && hasskill suckerpunch 
				skill suckerpunch
			endif
		endif
		if hasskill Wrath of the Lightning God
			skill Wrath of the Lightning God
			if hasskill sing
				skill sing
			endif
			if !hasskill sing && hasskill suckerpunch 
				skill suckerpunch
			endif
		endif
	endif
	if muscleclass
		if hasskill wrath of the trickster god
			skill wrath of the trickster god
			if hasskill toss
				skill toss
			endif
			if !hasskill toss && hasskill clobber
				skill clobber
			endif
		endif
		if hasskill Wrath of the Lightning God
			skill Wrath of the Lightning God
			if hasskill toss
				skill toss
			endif
			if !hasskill toss && hasskill clobber
				skill clobber
			endif
		endif
	endif
	if mysticalityclass
		if hasskill wrath of the trickster god
			skill wrath of the trickster god
			if hasskill boil
				goto jarlsbergfight
			endif
			if hasskill salsaball
				skill salsaball
			endif
			if !hasskill salsaball && hasskill spaghetti spear
				skill spaghetti spear
			endif
		endif
		if hasskill Wrath of the Volcano God
			skill Wrath of the Volcano God
			if hasskill boil
				goto jarlsbergfight
			endif
			if hasskill salsaball
				skill salsaball
			endif
			if !hasskill salsaball && hasskill spaghetti spear
				skill spaghetti spear
			endif
		endif
	endif
	#end of juju mask correction
endif #end of bonerdagon/ns

	
	
mark kill_the_mob
	#use fist skills
	if hasskill flying fire fist
		if hasskill chilled monkey brain technique
			skill  monkey brain technique
		endif
		#use stinkpalm if needed/better
		while hasskill stinkpalm && (monstername Battlie Knight Ghost || monstername ghost miner || monstername Claybender Sorcerer Ghost || monstername Dusken Raider Ghost || monstername Space Tourist Explorer Ghost || monstername Whatsian Commando Ghost || monstername chalkdust wraith || monstername ghost miner || monstername hulking construct || monstername Snow Queen || monstername ancient protector spirit || monstername Protector Spectre || monstername The Ghost of Fernswarthy || monstername banshee librarian || monstername chalkdust wraith || monstername empty suit of armor || monstername floating platter of hors d'oeuvres || monstername ghost miner || monstername skeletal cat || monstername skeletal hamster || monstername skeletal monkey || monstername skullery maid || monstername spooky mummy || monstername spooky vampire || monstername swarm of scarab beatles || monstername tapdancing skeleton || monstername tomb servant || monstername undead elbow macaroni || monstername zombie waltzers || monstername The Bonerdagon || monstername conjoined zmombie || monstername corpulent zobmie || monstername gargantulihc || monstername gaunt ghuol || monstername giant skeelton || monstername gluttonous ghuol || monstername grave rober zmobie || monstername huge ghuol || monstername senile lihc || monstername slick lihc || monstername spiny skelelton || monstername toothy sklelton || monstername flame-broiled meat blob || monstername Knob Goblin Barbecue Team || monstername overdone flame-broiled meat blob || monstername swarm of fire ants || monstername BL Imp || monstername CH Imp || monstername Demoninja || monstername Fallen Archfiend || monstername G imp || monstername Hellion || monstername inkubus || monstername L imp || monstername P imp || monstername Pr Imp || monstername serialbus || monstername suckubus || monstername W imp)
			skill stinkpalm
		endwhile
		while hasskill Seven-Finger Strike
			skill Seven-Finger Strike
		endwhile
		while hasskill drunken baby style
			skill drunken baby style
		endwhile
		while !missed 1
			skill flying fire fist
		endwhile
	endif
	
	#moxie kill
	if moxieclass
		#build up momentum
		if discobandit
			if hasskill stealth mistletoe
				skill stealth mistletoe
			endif
			if hasskill Disco Dance of Doom
				skill Disco Dance of Doom
			endif
			if hasskill Disco Dance II: Electric Boogaloo
				skill Disco Dance II: Electric Boogaloo
			endif
			if hasskill Disco Dance 3: Back in the Habit
				skill Disco Dance 3: Back in the Habit
			endif
		endif
		if hasskill throw shield
			skill throw shield
		endif
		while !missed 2
			attack
		endwhile
		abort
	endif
	#muscle kill
	if muscleclass
		if hasskill throw shield
			skill throw shield
			attack
		endif

		while hasskill cleave
			skill cleave
		endwhile
		while hasskill ravenous pounce && !monstername *naughty sorceress
			skill rave
		endwhile
		while hasskill shieldbutt && !monstername *naughty sorceress
			skill shieldbutt
		endwhile

		while !missed 2
			attack
		endwhile
		abort
	endif
	#myst kill
	if mysticalityclass
	
		#saucespam
		if sauceror
			if monsterhpabove 75
				skill Wave of Sauce
			endif
			if hasskill throw shield
				skill throw shield
			endif
			skill Saucegeyser
			
			while !missed 2
				if monsterhpabove 75
					skill Wave of Sauce
				endif
				skill Saucegeyser
			endwhile 
		endif
		#AOJ
		if hasskill curdle
			if hasskill throw shield
				skill throw shield
			endif

			#stun
			if hasskill blend && (!monstername oil baron && !monstername oil cartel && !monstername oil slick && !monstername oil tycoon)
				skill blend
			endif

			#spam strongest direct attack
			if hasskill grill
				skill grill
				repeat
			endif
			if hasskill slice && (!monstername Battlie Knight Ghost && !monstername ghost miner && !monstername Claybender Sorcerer Ghost && !monstername Dusken Raider Ghost && !monstername Space Tourist Explorer Ghost && !monstername Whatsian Commando Ghost && !monstername chalkdust wraith && !monstername ghost miner && !monstername hulking construct && !monstername Snow Queen && !monstername ancient protector spirit && !monstername Protector Spectre && !monstername The Ghost of Fernswarthy)
				skill slice
				repeat
			endif
			if hasskill fry && (!monstername eXtreme Orcish snowboarder && !monstername eXtreme Sports Orcs && !monstername Gnollish Crossdresser && !monstername Greasy Duck &&  !monstername Killer clownfish && !monstername Knob Goblin Harem Girl && !monstername pygmy headhunter && !monstername shaky clown && !monstername smarmy pirate && !monstername Axe Wound && !monstername Beaver && !monstername Box && !monstername Burrowing Bishop && !monstername Bush && !monstername Camel's Toe && !monstername Family Jewels && !monstername Flange && !monstername Honey Pot && !monstername Hooded Warrior && !monstername Junk && !monstername Little Man in the Canoe && !monstername Muff && !monstername One-Eyed Willie && !monstername Pork Sword && !monstername Skinflute && !monstername Trouser Snake && !monstername Twig and Berries && !monstername Orcish Frat Boy && !monstername caveman frat boy && !monstername caveman frat pledge && !monstername caveman sorority girl && !monstername Beer Bongadier && !monstername Elite Beer Bongadier && !monstername Heavy Kegtank && !monstername Naughty Sorority Nurse && !monstername Panty Raider Frat Boy && !monstername Sorority Nurse && !monstername Sorority Operator && !monstername War Frat 110th Infantryman && !monstername War Frat 151st Captain && !monstername War Frat 151st Infantryman && !monstername War Frat 500th Infantrygentleman && !monstername War Frat Elite 110th Captain && !monstername War Frat Elite 500th Captain && !monstername War Frat Elite Wartender && !monstername War Frat Grill Sergeant && !monstername War Frat Kegrider && !monstername War Frat Mobile Grill Unit && !monstername War Frat Senior Grill Sergeant && !monstername War Frat Wartender && !monstername Next-generation Frat Boy && !monstername oil baron && !monstername oil cartel && !monstername oil slick && !monstername oil tycoon && !monstername the man)
				skill fry
				repeat
			endif
			if hasskill freeze
				skill freeze
				repeat
			endif
			if hasskill chop && (!monstername Battlie Knight Ghost && !monstername ghost miner && !monstername Claybender Sorcerer Ghost && !monstername Dusken Raider Ghost && !monstername Space Tourist Explorer Ghost && !monstername Whatsian Commando Ghost && !monstername chalkdust wraith && !monstername ghost miner && !monstername hulking construct && !monstername Snow Queen && !monstername ancient protector spirit && !monstername Protector Spectre && !monstername The Ghost of Fernswarthy)
				skill chop
				repeat
			endif
			if hasskill boil             &&              (!monstername  flame-broiled meat blob && !monstername  Knob Goblin Barbecue Team && !monstername  overdone flame-broiled meat blob && !monstername  swarm of fire ants && !monstername  BL Imp && !monstername  CH Imp && !monstername  Demoninja && !monstername  Fallen Archfiend && !monstername  G imp && !monstername  Hellion && !monstername  inkubus && !monstername  L imp && !monstername  P imp && !monstername  Pr Imp && !monstername  serialbus && !monstername  suckubus && !monstername  W imp)
				skill boil
				repeat
			endif
			if hasskill curdle
				skill curdle
				repeat
			endif
		endif
		#pastamancer
		if hasskill weapon of the pastalord
			if hasskill throw shield
				skill throw shield
			endif
			skill weapon of the pastalord
			
			while !missed 2
				skill weapon of the pastalord
			endwhile 
		endif
	endif
abort



mark gremlinfight
	#banish
	if monstername A.M.C. gremlin
		if hasskill banishing shout
			skill banishing shout
		endif
		if hasskill howl of the alpha
			skill howl of the alpha
		endif
		if hasskill creepy grin
			skill creepy grin 
		endif
		if hasskill give your opponent the stinkeye
			skill give your opponent the stinkeye
		endif
		if hascombatitem divine champagne popper
			use divine champagne popper
		endif
		if hascombatitem crystal skull
			use crystal skull
		endif
		if hascombatitem harold's bell
			use harold's bell
		endif
		runaway
	endif
	if match "whips out"
		use molybdenum magnet
	endif
	if match "a bombing run over your head" || match "eyeball-peeler" || match "bites you in the fibula" || match "picks a beet off of itself" || match "picks a radish off of itself" || pastround 23
		if hascombatitem rain-doh
			use rain-doh indigo cup
			use rain-doh blue balls
		endif
		goto kill_the_mob
	endif
	if hppercentbelow 50
		if hasskill devour minions
			skill devour minions
		endif
	endif
	use spectre scepter
goto gremlinfight

